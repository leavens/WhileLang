grammar edu.ucf.cs.whilelang.WhileLang with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// the following is not real on purpose
generate whileLang "http://www.ucf.edu/cs/whilelang/WhileLang"

Program: 'proc' name=ID '(' (args=Formals)? ')' 'is' body=Stmt;
Formals: names+=ID (',' names+=ID)*;

Stmt returns S: Assignment | Skip | Block | While | If;
Block returns CompoundS: '{' stmts+=Stmt (';' stmts+=Stmt)* '}';
Assignment returns AssignS: (v=ID) ':=' (aexp=Expression) 
    | '[' (v=ID) ':=' (aexp=Expression) ']' '^' (label=INT);
Skip returns SkipS: 'skip' {SkipS} | '[' 'skip' ']' '^' (label=INT);
While returns WhileS: 'while' (bexp=LabeledExp) 'do' (block=Block);
If returns IfS: 'if' (bexp=LabeledExp) 'then' (s1=Block) 'else' (s2=Block);
LabeledExp returns LabeledExp: (be=Expression) | '[' (be=Expression) ']' '^' (label=INT);

Expression returns Expr: BDisj;
BDisj returns Expr: BConj ({BDisj.left=current} op=OR right=BConj)*;
BConj returns Expr: BRelExp ({BConj.left=current} op=AND right=BRelExp)*;
BRelExp returns Expr: AExpression ({BRelExp.left=current} op=OP_R right=AExpression)?;
AExpression returns Expr: Factor ({AExpression.left=current} op=OPPLUS right=Factor)*;
Factor returns Expr: Primary ({Factor.left=current} op=OPMUL right=Primary)*;
Primary returns Expr: VarRefExpr | NumLitExpr | BoolLitExpr | NotExpr | '(' Expression ')';
VarRefExpr: vname=ID;
NumLitExpr: num=INT;
BoolLitExpr: bval='true' | bval='false';
NotExpr: 'not' (bexp=Primary);

ElementaryBlock: Assignment | Skip | LabeledExp ;

@Override
terminal SL_COMMENT: '%' !('\n'|'\r')* ('\r'? '\n')?;
@Override
terminal INT returns ecore::EInt: ('0'..'9')+ | '-'('0'..'9')+;
terminal OPPLUS: '+' | '-';
terminal OPMUL: '*' | '/';
terminal OR: 'or';
terminal AND: 'and';
terminal OP_R: '=='| '!=' | '<=' | '>=' | '<' | '>' ;
