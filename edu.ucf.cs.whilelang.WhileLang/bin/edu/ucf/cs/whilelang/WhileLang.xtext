grammar edu.ucf.cs.whilelang.WhileLang with org.eclipse.xtext.common.Terminals

generate whileLang "http://www.ucf.edu/cs/whilelang/WhileLang"

Program: 'proc' name=ID '(' args=Formals ')' 'is' body=Stmt;
Formals: names+=ID (',' names+=ID)*;

Stmt returns S: Assignment | Skip | Block | While | If;
Block returns CompoundS: '{' stmts+=Stmt (';' stmts+=Stmt)* '}';
Assignment returns AssignS: (v=ID) ':=' (aexp=Expression) 
    | '[' (v=ID) ':=' (aexp=Expression) ']' '^' (label=INT);
Skip returns SkipS: 'skip' {SkipS} | '[' 'skip' ']' '^' (label=INT);
While returns WhileS: 'while' (bexp=LabeledExp) 'do' (block=Block);
If returns IfS: 'if' (bexp=LabeledExp) 'then' (s1=Block) 'else' (s2=Block);
LabeledExp returns LabeledExp: (be=Expression) | '[' (be=Expression) ']' '^' (label=INT);

Expression returns Expr: BDisj;
BDisj returns Expr: BConj ({BDisj.left=current} op=OR right=Expression)?;
BConj returns Expr: BRelExp ({BConj.left=current} op=AND right=BConj)?;
BRelExp returns Expr: AExpression ({BRelExp.left=current} op=OP_R right=AExpression)?;
AExpression returns Expr: Factor ({AExpression.left=current} op=OPPLUS right=AExpression)?;
Factor returns Expr: Primary ({Factor.left=current} op=OPMUL right=Factor)?;
Primary returns Expr: VarRefExpr | NumLitExpr | BoolLitExpr | NotExpr | '(' Expression ')';
VarRefExpr: vname=ID;
NumLitExpr: (negated?='-')? num=INT;
BoolLitExpr: bval='true' | bval='false';
NotExpr: 'not' (bexp=Primary);

@Override
terminal SL_COMMENT: '%' !('\n'|'\r')* ('\r'? '\n')?;
terminal OPPLUS: '+' | '-';
terminal OPMUL: '*' | '/';
terminal OR: 'or';
terminal AND: 'and';
terminal OP_R: '=='| '!=' | '<=' | '>=' | '<' | '>' ;
